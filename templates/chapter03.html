<style>
  h2 {
    counter-increment:count1;
    counter-reset:count2;
  }
  h2:before {
    content:"3." counter(count1) " ";
  }

  h3 {
    counter-increment:count2;
  }
  h3:before {
    content:"3." counter(count1) "." counter(count2) " ";
  }
</style>

<div class="chapter">
<div class="chapter_description">
  <div class="chapter_description_left">
    <img width="65" src="images/chapter_top.png">
  </div>
  <div class="chapter_description_right">
    Perlの文字列処理の関数を解説するね。
  </div>
</div>

<h2>join関数 - 区切り文字で配列を結合</h2>



<strong>join関数</strong>を使用すると、区切り文字を指定して<a href="/blog/20161012147627.html">配列</a>を結合することができます。

<pre>
my $joined = join(',', @array);
</pre>

第1引数は、区切り文字です。この例では区切り文字としてカンマを指定しています。第2引数以降で与えられた配列に含まれる文字列が区切り文字で結合されます。

次の簡単な例を見てください。join関数の使い方の簡単な例です。配列に含まれる文字列をカンマで連結してCSVデータを作成しています。

<pre>
# 配列から「Yuki,Toro,Kato」を作成
my @names = ('Yuki', 'Taro', 'Kato');
my $names_csv = join(',', @names);
</pre>

区切り文字はなんでもよく、タブ文字「\t」もよく利用されます。

<pre>
# 配列から「Yuki　Toro　Kato」を作成
my @names = ('Yuki', 'Taro', 'Kato');
my $names_csv = join("\t", @names);
</pre>

文字列の中にタブが含まれていることは少ないので、カンマよりも扱いやすいことが多いです。

改行を最後につけるとタブ区切りの一行のデータが簡単に作れます。

<pre>
my $line = join("\t", @names) . "\n";
</pre>

<h4>区切り文字を指定して配列に分解</h4>

<a href="/blog/20080301120434.html">split関数</a>を使えば、join関数と反対の操作、すなわち、区切り文字を指定して、配列に分解することができます。

<pre>
my @names = split(/,/, "Yuki,Taro,Kato");
</pre>

<h4>結合する配列にデータを追加するにはpush関数</h4>

結合するデータを追加したい場合は、<a href="/blog/20100106126425.html">push関数</a>を使うとよいでしょう。

<pre>
push @names, 'Kenta';
</pre>

<h3>join関数のサンプル</h3>

join関数を使って、文字を「タブ」で連結するサンプルです。

<pre>
use strict;
use warnings;

# 複数の文字列をタブで連結
my @animals = qw(cat dog sheep);
my $line = join("\t", @animals);
</pre>

参考:<a href="/blog/20080221120361.html">qw() 文字列連結演算子</a>


<h2>length関数 - 文字列の長さを取得</h2>



<strong>length関数</strong>を使用すると<a href="/blog/20161003147558.html">文字列</a>の長さを取得するすることができます。

<pre>
$ret = length $str;
</pre>

たとえば「abcde」という文字列の長さは5になります。

<pre>
# 文字列の長さは5
my $length = length 'abcde';
</pre>

<h4>日本語の文字列の長さを正しく取得する</h4>

length関数は文字列の長さを返却しますが、日本語の文字列の長さを正しく取得するためには、覚えておかなければならないことがひとつあります。

それは、内部文字列に対して、length関数を使う必要があるということです。内部文字列というのはEncodeモジュールのdecode関数でデコードされた文字列のことです。

内部文字列については、以下の記事を参考にしてください。

<ul>
  <li>
    <a href="/blog/20091118124667.html">Encodeモジュール - 日本語などのマルチバイト文字列を適切に処理する</a>
  </li>
</ul>

以下にサンプルプログラムを載せておきます。

日本語のようなマルチバイトで表現される文字の場合は、Encodeモジュールのdecode関数で内部文字列に変換してから、length関数を使用すると、正しい結果が得られます。全角日本語を処理する場合も同じ考え方です。

<pre>
use strict;
use warnings;

use Encode 'decode';

# 引数からUTF-8でエンコードされている文字列を取得
my $bytes = shift;

# 文字列の長さを取得
my $string = decode('UTF-8', $bytes);
my $length = length $string;
</pre>

<h4>lengthでは配列の長さは取得できない</h4>

Perlのlength関数は文字列の長さを取得する関数で<a href="/blog/20161012147627.html">配列</a>の長さを取得する関数ではないので注意してください。

配列の長さを取得するには、配列をスカラコンテキストで評価する必要があります。たとえば以下のようにします。

<pre>
# 配列の個数
my $count = @nums;
</pre>

スカラコンテキストについての詳しい解説は以下の記事をご覧ください。

<ul>
  <li>
    <a href="/blog/20080608121292.html">Perlのコンテキストについて理解する</a>
  </li>
</ul>

<h4>Perl 5.10未満のlengthは未定義値を渡したときに警告を発生させる</h4>

Perl 5.10未満のlengthは未定義値を渡したときに警告を発生させます。なので、汎用的なモジュールでlengthを書きたい場合は次のように書くと警告が出ないようになります。

<pre>
my $has_length = defined $foo && length $foo;
</pre>

定義されているかどうかを確認してからlength関数に渡すようにする。

反対にPerl 5.12以上であれば、定義されていてかつ長さを持っているということがlength関数ひとつでかけるようになったとうことです。

<pre>
my $has_length = length $foo;
</pre>

業務アプリケーションで最新のPerlを使っている場合は、上記のように書くと簡潔です。

<h3>サンプルプログラム</h3>

length関数を使って、文字列の長さを取得するサンプルです。

<pre>
# 文字列の長さを取得
my $message = "I like peace";
my $length = length $message;
</pre>


<h2>substr関数 - 文字列の切り出し・置換</h2>



<strong>substr関数</strong>を使用すると、指定した位置の文字を切り出す、あるいは置換することができます。第1引数は文字列、第2引数は開始位置、第3引数は切り出す長さです。開始位置は0から始まります。第3引数を省略した場合は<a href="/blog/20161003147558.html">文字列</a>の末尾までが対象になります。

<pre>
# 切り出し
my $word_like = substr($message, 2, 4);
</pre>

指定した位置の文字を置換するするには、第４引数に文字列を指定します。

<pre>
# 置換
substr($message, 2, 4, 'want to eat');
</pre>

<h4>substr関数のサンプルプログラム</h4>

substr関数を使って、指定した位置の文字を切り出すサンプルです。以下の例では文字列「Ken」が抽出されます。

<pre>
# 指定した位置の文字を切り出す。切り出される文字列は「Ken」。
my $message = "I'm Ken";
my $name = substr($message, 4, 3);
</pre>

substr関数を使って、指定した位置の文字列を置換するサンプルです。

<pre>
# 指定した位置の文字を置換する。置換後は「I'm Mike」になる。
my $message = "I'm Ken";
substr($message, 4, 3, 'Mike');
</pre>

<h4>日本語を扱いたい場合</h4>

substr関数で、日本語を正しく扱うには、文字列が内部文字列になっている必要があります。内部文字列については、以下の記事をご覧ください。

<ul>
  <li>
    <a href="/blog/20091118124667.html">Encodeモジュール - 日本語などのマルチバイト文字列を適切に処理する</a>
  </li>
</ul>

<h4>正規表現で置換する</h4>

文字列の置換は<a href="/blog/20100827127859.html">正規表現</a>を使って行うこともできます。文字の位置ではなく、対象の文字列を指定したい場合は、正規表現を使いましょう。

<pre>
# 正規表現を使ってKenをTaroに置換
my $message = "I'm Ken";
$message =~ s/Ken/Taro/;
</pre>


<h2>index関数 - 文字の検索</h2>

<strong>index関数</strong>を使用すると<a href="/blog/20161003147558.html">文字列</a>を検索することができます。

第1引数には検索対象の文字列、第2引数には検索したい文字列を指定します。第3引数には検索開始位置を指定することができます。省略した場合は文字列の最初から検索が行われます。見つかればその位置を、見つからなければ-1が返却されます。

<pre>
my $pos = index($target, $str);
</pre>

<h4>2回目に出現する文字を検索する</h4>

<pre>
my $word_love_pos_second
  = index($message, 'love', $word_love_pos_first + 1);
</pre>

indexの第3引数に、検索開始位置を指定することができる。1回目の検索で、発見した位置の次の位置を指定する。

<h4>検索した文字のすべての位置を取得する</h4>

<pre>
{
  my @found_positions = ();
  my $found_posision = -1;
    
  while (1) { 
    $found_posision = index($message, 'love', $found_posision + 1);
    last if $found_posision == -1;
    push @found_positions, $found_posision;
  }
    
  print "見つかった位置: " . join(', ', @found_positions) . "\n";
}
</pre>

<a href="/blog/20100123126425.html">while文</a>で、戻り値が-1になるまで,index関数を繰り返す。

<h4>日本語を扱いたい場合</h4>

index関数で、日本語を正しく扱うには、文字列が内部文字列になっている必要があります。内部文字列については、以下の記事をご覧ください。

<ul>
  <li>
    <a href="/blog/20091118124667.html">Encodeモジュール - 日本語などのマルチバイト文字列を適切に処理する</a>
  </li>
</ul>

<h4>正規表現で検索する</h4>

文字列の検索は<a href="/blog/20100827127859.html">正規表現</a>を使って行うこともできます。文字の位置ではなく、対象の文字列を指定したい場合は、正規表現を使いましょう。

<pre>
# 正規表現を使ってKenが存在するか調べる
my $message = "I'm Ken";
if ($message =~ /Ken/) {
  ...
}
</pre>

<h3>index関数を使ったプログラミングのサンプル</h3>

index関数を使った、カンマの最初の位置を調べるサンプルです。

<pre>
# カンマの最初の位置を調べる
my $csv = 'aaa,bbb,ccc';
my $pos = index($csv, ',');
</pre>

文字列が見つかった場合にのみ処理を行うサンプルです。

<pre>
# 文字列が見つかった場合にのみ処理を行う
my $string = "I'm Ken";
my $search = 'Ken';

if (index($string, $search) != -1) {
  print "Found\n";
}
</pre>

その他のサンプルです。

<pre>
use strict;
use warnings;

my $message = "love love love.";

# index関数を使って文字を検索
my $word_love_pos_first = index($message, 'love');

# 二つ目のloveを検索
my $word_love_pos_second = index($message, 'love', $word_love_pos_first + 1);

print "First: $word_love_pos_first, Second: $word_love_pos_second\n";

# 繰り返し検索
{
  my @found_positions = ();
  my $found_posision = -1;
    
  while (1) { 
    $found_posision = index($message, 'love', $found_posision + 1);
    last if $found_posision == -1;
    push @found_positions, $found_posision;
  }

  print "Found: " . join(', ', @found_positions) . "\n";
}
</pre>

出力結果

<pre>
First: 0, Second: 5
Found: 0, 5, 10
</pre>


<h2>rindex関数 - 末尾から文字列を検索する</h2>

<strong>文字列の末尾から文字列を検索</strong>するには<strong>rindex関数</strong>を使用します。

<pre>
my $pos = rindex($target, $str);
</pre>

<a href="/blog/20080225120395.html">index関数</a>が先頭から文字列の検索を行うのに対して、rindex関数は、末尾から文字列を検索します。第1引数には検索対象の文字列、第2引数には検索したい文字列を指定します。第3引数には検索開始位置を指定することができます。省略した場合は文字列の末尾から検索が行われます。見つかればその位置を、見つからなければ-1が返却されます。

<h3>rindex関数を使ったプログラミングのサンプル</h3>

rindex関数を使ったプログラミングのサンプルです。

<pre>
# 文字を文字列の最後から検索する
my $message = "love love love.";
my $word_love_pos_last = rindex($message, 'love');
</pre>


<h2>split関数 - 文字列の分割</h2>



<strong>split関数</strong>を使用すると区切り文字を指定して<a href="/blog/20161003147558.html">文字列</a>を分割することができます。

<pre>
my @ret = split(/,/, $person_csv);
</pre>

第1引数には区切り文字を<a href="/blog/20100827127859.html">正規表現</a>で指定します。第2引数には分割したい文字列を指定します。戻り値は分割された文字列の<a href="/blog/20161012147627.html">配列</a>です。

<h4>正規表現を使う</h4>

split関数は、第一引数に正規表現を受け取るので、複数の空白で分割ということも簡単にできます。

<pre>
my @ret = split(/ +/, $line);
</pre>

<h4>文字列の連結</h4>

split関数は、文字列を分解しますが、その反対の操作として、文字列の連結を行いたい場合は、join関数を使用します。join関数については以下の記事をご覧ください。

<ul>
  <li>
    <a href="/blog/20081007122338.html">join関数 - 区切り文字で配列を結合</a>
  </li>
</ul>

<h3>サンプル</h3>

split関数のサンプルプログラムです。

<h4>CSVデータ</h4>

CSV形式のデータを配列に変換するサンプルです。

<pre>
# CSV形式のデータを配列に変換
my $string = 'a,b,c';
my @record = split /,/, $string;
</pre>

<h4>空白区切り</h4>

空白で区切られたデータ配列に変換するサンプルです。

<pre>
# 空白で区切られたデータを配列に変換
my $string = 'a   b     c';
my @record = split /\s+/, $string;
</pre>

<h4>カンマ区切り</h4>

カンマ区切りの文字列を処理するサンプルです。

<pre>
use strict;
use warnings;

# 1: カンマ区切りの文字列を処理する。
my $header_csv = "name,age,country\n";
my $person_csv = "kaori,23,Japan\n";

# chomp関数で改行を取り除く
chomp $header_csv;
chomp $person_csv;

# split関数を用いて、区切り文字にカンマを指定する。
# split( /区切り文字/, 文字列 )
my @header = split(/,/, $header_csv);
my @person_attribute = split(/,/, $person_csv);

print "1: カンマ区切りの文字列を処理する。\n";
print "\$header_csv = $header_csv\n";
print "\$person_csv = $person_csv\n";
print "\n";

for my $i (0 .. 2) {
  print "\$header[ $i ] = " . $header[$i] . "\n";
  print "\$person_attribute[ $i ] = " . $person_attribute[$i] . "\n\n";
}
</pre>

実行結果

<pre>
1: カンマ区切りの文字列を処理する。
$header_csv = name,age,country
$person_csv = kaori,23,Japan

$header[ 0 ] = name
$person_attribute[0] = kaori

$header[ 1 ] = age
$person_attribute[1] = 23

$header[ 2 ] = country
$person_attribute[2] = Japan
</pre>


<h2>reverse関数 - 配列を逆順(文字列を逆順)にする</h2>



<strong>reverse関数</strong>を使用すると、<a href="/blog/20161003147558.html">文字列</a>を逆順あるいは<a href="/blog/20161012147627.html">配列</a>を逆順にすることができます。

<pre>
# 文字列を逆順にする
$str_reverse = reverse $str;

# 配列を逆順にする
@array_reverse = reverse @array;
</pre>

<a href="/blog/20161013147635.html">ハッシュ</a>とキーの値を入れ替えるのに、reverse関数を使用することもできます。

<pre>
%hash_reverse = reverse %hash;
</pre>

<h3>サンプル</h3>

reverse関数を使って、文字列を逆順に並べ替えるサンプルです。

<pre>
# 文字列を逆順に並べ替え
my $string = 'abc';
$string = reverse($string);
</pre>

配列を逆順に並べ替えるサンプルです。

<pre>
# 配列を逆順に並べ替え
my @nums = (1, 2, 3);
@nums = reverse @nums;
</pre>

reverse関数を使ってハッシュのキーと値を入れ替えるサンプルです。

<pre>
use strict;
use warnings;

use Data::Dumper;
my %x_to_y = (
  'x1' => 'y1',
  'x2' => 'y2',
  'x3' => 'y3',
);

print "元のハッシュ\n";
print Data::Dumper->Dump([\%x_to_y], ['*x_to_y']);
print "\n";

# 1: ハッシュのキーと値を入れ替える。
# ハッシュに対して,reverse関数を用いる。
my %y_to_x = reverse %x_to_y;
print "1: ハッシュのキーと値を入れ替える\n";
print Data::Dumper->Dump([\%y_to_x], ['*y_to_x']);
</pre>

実行結果

<pre>
元のハッシュ
%x_to_y = (
            'x3' => 'y3',
            'x2' => 'y2',
            'x1' => 'y1'
          );

1: ハッシュのキーと値を入れ替える
%y_to_x = (
            'y1' => 'x1',
            'y3' => 'x3',
            'y2' => 'x2'
          );
</pre>


<h2>uc関数 - 小文字を大文字に変換する</h2>

<strong>uc関数</strong>を使用すると小文字から大文字へ変換することができます。uc関数は引数のそれぞれの文字を大文字に変換します。変換できない文字の場合は引数に与えた文字がそのまま返されます。

<pre>
my $str = uc "aaa";
</pre>

<h3>uc関数のプログラミングのサンプル</h3>

uc関数を使った小文字から大文字への変換のプログラミングのサンプルです。

<pre>
use strict;
use warnings;

# 小文字「aaa」から大文字「AAA」への変換
my $str = uc "aaa";

print "$str\n";
</pre>

出力結果

<pre>
AAA
</pre>

<h3>uc関数の関連情報</h3>

uc関数の関連情報です。

<ul>
  <li><a href="/blog/20100326127089.html">lc関数 - 大文字を小文字に変換する</a>
  <li><a href="/blog/20100327127089.html">lcfirst関数 - 文字列の先頭を大文字から小文字に変換する</a>
  <li><a href="/blog/20100325127089.html">ucfirst関数 - 文字列の先頭を小文字から大文字に変換する</a>
</ul>

<h2>ucfirst関数 - 文字列の先頭を小文字から大文字に変換する</h2>

<strong>ucfirst</strong>関数を使用すると、文字列の先頭だけを小文字から大文字に変換することができます。

<pre>
# 先頭の文字を大文字に変換
$ret = ucfirst $string;
</pre>

<h3>ucfirst関数のプログラミングのサンプル</h3>

ucfirst関数のプログラミングのサンプルです。

<pre>
use strict;
use warnings;

# "foo"を"Foo"に変換
my $str = ucfirst "foo";

print "$str\n";
</pre>

出力結果

<pre>
Foo
</pre>

<h3>ucfirst関数の関連情報</h3>

ucfirst関数の関連情報です。

<ul>
  <li><a href="/blog/20100326127089.html">lc関数 - 大文字を小文字に変換する</a>
  <li><a href="/blog/20100327127089.html">lcfirst関数 - 文字列の先頭を大文字から小文字に変換する</a>
  <li><a href="/blog/20080903122057.html">uc関数 - 小文字を大文字に変換する</a>
</ul>


<h2>lc関数 - 大文字を小文字に変換する</h2>

<strong>lc関数</strong>を使用すると、<strong>大文字から小文字へ変換</strong>することができます。lc関数は引数のそれぞれの文字を小文字に変換します。変換できない文字の場合は引数に与えた文字がそのまま返されます。

<pre>
my $str = lc "BBB";
</pre>

<h3>lc関数のプログラミングのサンプル</h3>

lc関数で大文字から小文字へ変換するプログラミングのサンプルです。

<pre>
use strict;
use warnings;

# 大文字「BBB」から小文字「bbb」への変換
my $str = lc "BBB";

print "$str\n";
</pre>

出力結果

<pre>
bbb
</pre>

<h3>lc関数の関連情報</h3>

lc関数の関連情報です。

<ul>
  <li><a href="/blog/20100327127089.html">lcfirst関数 - 文字列の先頭を大文字から小文字に変換する</a>
  <li><a href="/blog/20080903122057.html">uc関数 - 小文字を大文字に変換する</a>
  <li><a href="/blog/20100325127089.html">ucfirst関数 - 文字列の先頭を小文字から大文字に変換する</a>
</ul>


<h2>lcfirst関数 - 文字列の先頭を大文字から小文字に変換する</h2>

<strong>lcfirst</strong>関数を使用すると、文字列の先頭だけを大文字から小文字に変換することができます。

<pre>
# 先頭の文字を大文字から小文字に変換
$ret = lcfirst $string;
</pre>

<h3>lcfirst関数のプログラミングのサンプル</h3>

lcfirst関数のプログラミングのサンプルです。

<pre>
use strict;
use warnings;

# "FOO"を"fOO"に変換
my $str = lcfirst "FOO";

print "$str\n";
</pre>

出力結果

<pre>
fOO
</pre>

<h3>lcfirst関数の関連情報</h3>

lcfirst関数の関連情報です。

<ul>
  <li><a href="/blog/20100326127089.html">lc関数 - 大文字を小文字に変換する</a>
  <li><a href="/blog/20080903122057.html">uc関数 - 小文字を大文字に変換する</a>
  <li><a href="/blog/20100325127089.html">ucfirst関数 - 文字列の先頭を小文字から大文字に変換する</a>
</ul>

<h2>sprintf関数 - 文字列の書式指定</h2>



<strong>sprintf関数</strong>を使用すると書式指定した<a href="/blog/20161003147558.html">文字列</a>を作成することができます。<a href="/blog/20161026147748.html">数値</a>の左側を0で埋めて出力したい場合や、浮動小数点の少数の桁数を指定したい場合などに、sprintf関数を使うと便利です。

<pre>
my $str = sprintf("%08d", $num1);
my $str = sprintf("%.2f", $num2);
my $str = sprintf("aiueo%08daiueo%.2f", $num1, $num2);
</pre>

数値や文字列をある規則にしたがって出力したい場合があります。たとえば、足りない部分を空白で埋めた固定長の文字列を作成したい場合 "18&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;" のようにわざわざ空白を作っておくのは面倒です。

sprintf関数で書式指定を行えば、 "18" を "18&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;" のように変換できます。

またたとえば足りない部分を0で埋めた数値を出力することもできます。 18 を '00000018' にすることができます。

小数点をある桁で丸めて出力することもできます。 12.3456 を 小数点第一位で丸めて '12.3' にできます。

<h3>書式指定の記述方法</h3>

<pre>
my $str = sprintf("%08d", $num1);
my $str = sprintf("%.2f", $num2);
my $str = sprintf("aiueo%08daiueo%.2f", $num1, $num2);
</pre>

書式指定は%で始まります。最後は d や f などの決められた英数字で終わります。dは第2引数を整数として解釈するという意味で、fは浮動小数点として解釈するという意味です。

% と 英文字 の間にはどのような書式で文字列を作成するかということを指定します。"%08d"は「$num1を整数として解釈して8文字に満たない場合は0で埋める」と読みます。"%.2f"は「$num2を浮動小数点として解釈して小数点以下2桁で丸める」と読みます。

<h3>sprintf関数の書式指定子一覧</h3>

sprintf関数には書式を設定するさまざまな指定子が存在します。一覧を作成しました。

<h4>%c</h4>

%cはASCII文字コードに対応する文字に変換するフォーマット指定子です。

<h4>%s</h4>

%sは引数を文字列として解釈するフォーマット指定子です。'002'のように先頭に0がついている場合は、そのまま文字列として'002'になります。

<h4>%d</h4>

%dは引数を符号付整数として解釈します。たとえば'0003'を渡した場合は、数値としてみれば 3であるので 3 に変換されます。

<h4>%u</h4>

%uは引数を正の整数として解釈します。

<h4>%o</h4>

%oは引数を正の整数として解釈して8進文字列に変換します。

<h4>%x %X</h4>

%xは引数を正の整数として解釈して16進文字列に変換します。%xの場合は表記が小文字に、%Xの場合は表記が大文字になります。

<h4>%b</h4>

%bは引数を正の整数として解釈して2進文字列に変換します。

<h4>%f</h4>

%fは引数を浮動小数点として解釈します。

<h4> %e %E</h4>

%eは%fと同じですが、指数表記にフォーマットしてくれます。%eの場合は表記が小文字に、%Eの場合は表記が大文字になります。

<h4>%g %G</h4>

%gは引数を浮動小数点として解釈して %e か %f にフォーマットします。小数点以下が短い場合などは、%fのフォーマットに、長い場合は%eのフォーマットになったりします。

また、%gは%f,%e と異なり末尾の余分な0はつきません。

<h4>%p</h4>

%pで引数で与えられた変数のメモリ上のアドレスを16進表記で取得できます。

<h3>固定長文字列にフォーマットする</h3>

<strong>sprintf</strong>関数で<strong>固定長文字列</strong>にフォーマットするには以下の例のような書式指定を使います。

<table>
  <tr>
    <td>
      %5s
    </td>
    <td>
      右詰めスペース埋め
    </td>
  </tr>

  <tr>
    <td>
      %-5s
    </td>
    <td>
      左詰めスペース埋め
    </td>
  </tr>

  <tr>
    <td>
      %0s
    </td>
    <td>
      0埋め
    </td>
  </tr>

</table>

sprintf関数で固定長文字列にフォーマットするには、%に続けて数値を記述します。%5s のように書きます。

デフォルトでは右詰めのスペース埋めになります。左詰めにするためには、数値の前にマイナス(-)をつけて%-5sのようにします。足りない部分を0で埋めるには、%05sのように記述します。

フォーマット後の文字列が指定した文字列超を越える場合は、切り詰められることはされず指定が無視されます。

<h3>2進数、8進数、16進数を進数表記でフォーマットする</h3>

2進数、8進数、16進数を数値リテラルとして表現するときに、0b, 0, 0X をつけて表現しました。2進数、8進数、16進数をこの表現にフォーマットするには、# 記号を使います。

<pre>
sprintf("1: %#o", 8);
sprintf("2: %#x", 255);
sprintf("3: %#X", 255);
sprintf("4: %#b", 4); 
</pre>

進数表記でフォーマットするには、#を使用します。

他のフォーマットと組み合わせることもできます。

<pre>
# 10進数を8進数に変換 左詰でスペース埋め
$formated[5]  = sprintf("1: <%-#5o>", 8);
</pre>

# というのはフォーマット指定の中で「フラグ」と呼ばれるものに分類されます。- も「フラグ」に分類されます。5は「文字列幅」と呼ばれるものに分類されます。

「フラグ」どうしの順序はどんな順序でもかまいませんが、「フラグ」は「文字列幅」よりも前にくる必要があります。

<pre>
"%-#5o" # OK
"%#-5o" # OK
"%5-#o" # 駄目
</pre>

<h3>小数点以下のけた数で精度を指定する</h3>

sprintf関数を使って小数点以下の桁数で精度を指定するには、フォーマット指定子 %f と .5 .10 のような「精度」の表記を使用します。

<pre>
# 小数点以下3位
$formated[1] = sprintf("%.3f", 0.45674);
</pre>

小数点以下の桁数で精度を指定するには、引数を浮動少数点として解釈するフォーマット指定子 %f と .5 .10 のような「精度」の表記を使用します。.3 とすると小数点以下第3位で丸められます。

他のフォーマットと組み合わせることもできます。

<pre>
# 左詰スペース埋めの文字幅10で小数点以下3位に丸める
$formated[2]  = sprintf("<%-10.3f>", 0.45674);
</pre>

<ul>
  <li>
     は「フラグ」と呼ばれるものに分類されます。10 は「文字列幅」と呼ばれるものに分類されます。.3 は「精度」と呼ばれるものに分類されます。fは公式に名前はないようですがここでは、フォーマット指定子と呼んでいます。
  </li>
</ul>

フォーマット指定の順番は、

<pre>
% フラグ 文字列幅 精度 フォーマット指定子
</pre>

である必要があります。「フラグ」「文字列」「精度」はフォーマット指定のオプションになっていて必要に応じて指定します。

<h3>有効桁数で精度を指定する</h3>

sprintf関数を使って有効桁数で精度を指定するには、フォーマット指定子 %g と .5 .10 のような「精度」の表記を使用します。

<pre>
# 有効桁3桁
my $formated = sprintf("%.3g", 45.674);
</pre>

小数点以下の桁数で精度を指定するには、引数を浮動少数点として解釈するフォーマット指定子 %g と .5 .10 のような「精度」の表記を使用します。%.3g とすると有効桁３けたで丸められます。

<h3>sprintf関数のサンプル</h3>

sprintf関数のサンプルです。

<h4>sprintfの簡単なサンプル</h4>

sprintf関数の簡単なサンプルです。

<pre>
use strict;
use warnings;

my $num1 = 3;

# 8桁で足りない部分は0で埋める
my $formated1 = sprintf("%08d", $num1); 
print $formated1 . "\n\n";

# 小数点以下2桁に丸める
my $num2 = 3.1415;
my $formated2 = sprintf("%.2f", $num2);
print $formated2 . "\n\n";

# 複数の引数を指定する
my $formated3 = sprintf("aiueo%08daiueo%.2f", $num1, $num2);
print $formated3 . "\n";
</pre>

<h4>さまざまな書式指定のサンプル</h4>

さまざまな書式指定のサンプルです。

<pre>
use strict;
use warnings;

print "(1)書式指定子一覧\n";
my @formated;

# 65 はASCIIコードで 'A'
$formated[1]  = sprintf("1: %c", 65);

# 文字列として解釈
$formated[2]  = sprintf("2: %s", '00002');

# 符号付整数として解釈 
$formated[3]  = sprintf("3: %d", '-00002');

# 符号なし整数として解釈
$formated[4]  = sprintf("4: %u", '00002');

# 10進数を8進数に変換
$formated[5]  = sprintf("5: %o", 8);

# 10進数を16進数に変換(小文字表記)
$formated[6]  = sprintf("6: %x", 255);

# 10進数を16進数に変換(大文字表記)
$formated[7]  = sprintf("7: %X", 255);

# 10進数を2進数に変換 
$formated[8]  = sprintf("8: %b", 4);

# 浮動少数点として解釈
$formated[9]  = sprintf("9: %f", 0.00004560);

# 浮動小数点として解釈
# (指数表記小文字)
$formated[10] = sprintf("10: %e",0.00004560);

# 浮動小数点として解釈
# (指数表記大文字)
$formated[11] = sprintf("11: %E",0.00004560);

# 浮動小数点として解釈
# (末尾0は削除される)
$formated[12] = sprintf("12: %g", 0.00004560);

# 浮動小数点として解釈
# (末尾の0は削除される。大文字)
$formated[13] = sprintf("13: %G", 0.00004560);

# $num のメモリ上のアドレス
my $num = 3;                                              
$formated[14] = sprintf("14: %p", $num); 

for my $i (1 .. 14) {
  print $formated[$i] . "\n"; 
}
</pre>

<h4>固定長文字列にフォーマットする</h4>

sprintf関数を使って固定長文字列にフォーマットするサンプルです。

<pre>
use strict;
use warnings;

print "(1)固定長文字列にフォーマットする。\n";
my @formated;

# 右詰めスペース埋め
$formated[1] = sprintf("1: %5s", 1);

# 左詰めスペース埋め
$formated[2] = sprintf("2: %-5s", 1);

# 0埋め
$formated[3] = sprintf("3: %05s", 1);

# 指定した文字の長さを超える場合は無視される
$formated[4] = sprintf("4: %5d", 123456);

# %s以外でもOK。
$formated[5] = sprintf("5: %5d", 1);

# %s以外でもOK。
$formated[6] = sprintf("6: %5g", 1.23);

for my $i (1 .. 6) {
  print $formated[$i] . "\n"; 
}
</pre>

<h4>2進数、8進数、16進数を進数表記でフォーマット</h4>

sprintfで2進数、8進数、16進数を進数表記でフォーマットするサンプルです。

<pre>
use strict;
use warnings;

print "(1)2進数、8進数、16進数を進数表\記でフォーマットする\n";
my @formated;

# 10進数を8進数に変換
$formated[1]  = sprintf("1: %#o", 8);

# 10進数を16進数に変換(小文字表記)
$formated[2]  = sprintf("2: %#x", 255);

# 10進数を16進数に変換(大文字表記)
$formated[3]  = sprintf("3: %#X", 255);

# 10進数を2進数に変換
$formated[4]  = sprintf("4: %#b", 4); 

for my $i (1 .. 4) {
  print $formated[$i] . "\n";
}
print "\n";

# 10進数を8進数に変換 左詰でスペース埋め
print "(2)他のフォーマットと組み合わせる。\n";
$formated[5]  = sprintf("1: <%-#5o>", 8);

print $formated[5] . "\n";
</pre>

<h4>小数点以下のけた数で精度を指定</h4>

小数点以下のけた数で精度を指定するサンプルです。

<pre>
use strict;
use warnings;

printf "(1)小数点以下のけた数で精度を指定する。\n";
my @formated;
# 小数点以下3位までの精度で
# フォーマットする。
$formated[1] = sprintf("%.3f", 0.45674);
print $formated[1] . "\n\n";

# 左詰スペース埋めの文字幅10で小数点以下3位に丸める
print "(2)他のフォーマットと組み合わせる。\n";
$formated[2]  = sprintf("<%-10.3f>", 0.45674);
print $formated[2] . "\n";
</pre>

<h4>有効桁数で精度を指定</h4>

有効桁数で精度を指定するサンプルです。

<pre>
use strict;
use warnings;

printf "(1)有効桁数で精度を指定する。\n";

# 有効桁3桁の精度でフォーマットする。
my $formated = sprintf("%.3g", 45.674);
print $formated . "\n";
</pre>

<h2>chomp関数 - 改行を削除する</h2>

<strong>chomp関数</strong>を使用すると、<b>改行を削除</b>することができます。

<pre>
# 末尾の改行を削除
chomp $line;
</pre>

chomp関数は、<a href="/blog/20161102147817.html">特殊変数</a>「$/」に設定されている改行文字を取り除きます。

Windows、<a href="https://linux.perlzemi.com/">Linux</a>/UNIX、Macでは16進数で「\x0A」です。旧Macでは16進数で「\x0D」です。

<h4>改行コードの表現に依存しないで改行を削除する</h4>

現実的に存在すると考えられる改行コードは「\x0A」「\x0D」「\x0D\x0A」の3種類です。

これらの改行コードを、その違いを意識しないで削除するには、<a href="/blog/20100827127859.html">正規表現</a>を使って、次のように記述します。

<pre>
# 環境に依存しないで末尾の改行を削除
$string =~ s/\x0D?\x0A?$//;
</pre>

<h3>サンプル</h3>

chomp関数を使ったサンプルです。

<pre>
use strict;
use warnings;

my $message = "I like a cake.\n";

# 行末の改行を削除する
chomp($message);
print $message;
</pre>

<h2>chr関数 - 数値をASCIIに変換</h2>



<strong>chr関数</strong>を使用すると数値をASCII(アスキー)コードに対応する文字に変換することができます。第１引数にASCIIコードを指定するとそれに対応する文字が戻り値で返ります。これは<a href="/blog/20080929122278.html">ord関数</a>の逆の演算です。

<pre>
my $char = chr $ascii_code;
</pre>

<h3>chr関数のサンプル</h3>

chr関数を使って、数値をASCIIコード対応する文字に変換するサンプルです。

<pre>
use strict;
use warnings;

# ASCII文字コードでは65,66,67はそれぞれ 'A','B','C'に対応する。
my $a_num = 65;
my $b_num = 66;
my $c_num = 67;

print chr($a_num) . "\n";
print chr($b_num) . "\n";
print chr($c_num) . "\n";
</pre>


<h2>ord関数 - ASCII文字を数値に変換</h2>



<strong>ord関数</strong>を使用すれば、ASCIIコードの文字を内部表現である数値に変換することができます。これは<a href="/blog/20080928122260.html">chr関数</a>と反対の変換です。

<pre>
my $ascii_code = ord $char;
</pre>

<h3>ord関数のサンプル</h3>

ord関数を使って、ASCIIコードの文字を内部表現である数値に変換するサンプルです。

<pre>
use strict;
use warnings;

print "(1)ASCIIコードの文字を内部表\現である数値に変換する。\n";

# ASCII文字コードでは65,66,67はそれぞれ 'A','B','C'に対応する。
my $A = 'A';
my $B = 'B';
my $C = 'C';

print ord($A) . "\n";
print ord($B) . "\n";
print ord($C) . "\n";
</pre>


<h2>oct関数 - 8進数文字列を10進数の数値に変換</h2>



<b>oct関数</b>を使用すると8進数文字列を10進数の数値に変換することができます。

<h4>8進数から10進数への変換</h4>

8進数から10進数へ変換するには<strong>oct</strong>関数を使用します。

<pre>
my $digit = oct('755');
</pre>

8進数の755は10進数の493になります。

上のサンプルでは'755'を渡していますが'0755'のように先頭に0がついていても正しく変換されます。

<h4>2進数から10進数への変換</h4>

oct関数を使って、2進数文字列を10進数の数値に変換することもできます。oct関数は本来は8進数を10進数に変換する関数ですが、0bが先頭についた文字列を引数として渡した場合は、2進数文字列を10進数の数値に変換します。

<pre>
my $digit = oct('0b101');
</pre>

<h4>進数変換についてもう少し詳しく</h4>

16進数から10進数に変換するには<a href="/blog/20171026150980.html">hex関数</a>を使うことができます。

2進数、8進数、16進数などの進数変換についてもう少し詳しく知りたい場合は、以下の記事を参考にしてください。

<ul>
  <li>
    <a href="/blog/20080901122026.html">Perlで進数変換を行う方法 - 2進数、8進数、16進数</a>
  </li>
</ul>

<h2>hex関数 - 16進数文字列を10進数の数値に変換</h2>



<b>hex関数</b>を使用すると16進数文字列を10進数の数値に変換することができます。

<h4>16進数から10進数への変換</h4>

16進数から10進数へ変換するには<strong>hex</strong>関数を使用します。

<pre>
my $digit = hex('FF');
</pre>

16進数のFFは10進数の255になります。

サンプルでは'FF'を引数に渡していますが'0xFF'のように先頭に0xがついていても正しく変換されます。

<h4>進数変換についてもう少し詳しく</h4>

8進数から10進数に変換するには<a href="/blog/20171027150945.html">oct関数</a>を使うことができます。

2進数、8進数、16進数などの進数変換についてもう少し詳しく知りたい場合は、以下の記事を参考にしてください。

<ul>
  <li>
    <a href="/blog/20080901122026.html">Perlで進数変換を行う方法 - 2進数、8進数、16進数</a>
  </li>
</ul>

<h2>crypt関数 - ダイジェストを作成してパスワードの照合を行う</h2>



crypt関数を使うと、パスワードをダイジェストにして保存しておくことができます。パスワードを平文で保存しておくと見られてしまいます。第一引数はパスワードです。第二引数はサルトという任意の文字列で、ダイジェストを解読されにくくするためのものです。

<pre>
my $digest = crypt($passwd, $salt);
</pre>

ダイジェストの作成を行うなら現在ならばcrypt関数よりも、<a href="/blog/20100501127089.html">Digest::MD5</a>や<a href="/blog/20100502127089.html">Digest::SHA</a>を使うのがお勧めです。

<h3>crypt関数のサンプル</h3>

crypt関数を使ったサンプルです。

<pre>
use strict;
use warnings;

# パスワードを作成
# [./0-9A-Za-z] からなるランダムな2文字をsaltとして作成する
print "(1)パスワードからダイジェストを作成する。\n";
my $passwd = 'secret';
my $salt = join '' . ('.', '/', 0 .. 9, 'A' .. 'Z', 'a' .. 'z')[rand 64, rand 64];
             
my $digest = crypt($passwd, $salt);
print "$digest\n\n";


print "(2)パスワードの照合を行う。\n";
if (crypt($passwd, $digest) eq $digest) {
  print "パスワードが一致しました。\n";
}
</pre>

<h4>パスワードからダイジェストを作成する</h4>

<pre>
my $passwd = 'secret'; # パスワードを作成
my $salt = join '', ('.', '/', 0 .. 9, 'A' .. 'Z', 'a' .. 'z')[rand 64, rand 64];
my $digest = crypt($passwd, $salt);
</pre>

パスワードからダイジェストを作成するにはcrypt関数を使用します。crypt関数は一方向ハッシュ関数です。一方向ハッシュによって変換された文字列をダイジェストと呼びます。

crypt関数によって作成されたダイジェストは複合することが非常に困難です。

cryptの第1引数には、ダイジェストの元になる文字列を指定します。最初の8バイトだけが意味を持ちます。それより長い文字は無視されます。

cryptの第2引数には、[./0-9A-Za-z]の64文字のうちの2文字を指定します。2文字目以降は無視されます。この64文字からランダムな2文字を選ぶようにするとダイジェストの安全性が高くなります。

作成されるダイジェストの先頭はこの2文字になります。

<h4>パスワードの照合を行う</h4>

<pre>
if (crypt($passwd, $digest) eq $digest){ }
</pre>

パスワードの照合を行うときは、crypt関数を使用して再びダイジェストを作成します。作成したダイジェストが、以前に作成されたダイジェストと一致すれば、正しいパスワードであることがわかります。

ダイジェストの先頭2文字には、$saltで指定された2文字ですから、crypt関数の第2引数にそのまま使用することができます。







